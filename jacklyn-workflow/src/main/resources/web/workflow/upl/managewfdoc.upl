// UPL template for managing workflow document definition
// Copyright 2018 The Code Department
!ui-page caption:$m{workflow.wfdoc.manage}
	searchByList:$c{searchWfCategoryId searchName searchDescription}
    searchClearList:$c{searchWfCategoryId searchName searchDescription}
	tableColumnList:$c{wfCategoryDesc name description}
	formSection:$d{!ui-section caption:$m{workflow.wfdoc.section.basicdetails} components:$c{frmWfCategoryId frmName frmDescription}}
	formSection:$d{!ui-section caption:$m{workflow.wfdoc.section.classifiers} components:$c{frmWfDocClassifierListPanel}}
	formSection:$d{!ui-section caption:$m{workflow.wfdoc.section.attachments} components:$c{frmWfDocAttachmentListPanel}}
	viewPrivilege:$s{wf-wfdoc-view}

// Search
!ui-select:searchWfCategoryId caption:$m{workflow.wfdoc.category}
	list:wfcategorylist blankOption:$s{} binding:searchWfCategoryId
	eventHandler:$d{!ui-event event:onchange action:$c{searchAct}}
!ui-text:searchName caption:$m{workflow.wfdoc.name} binding:searchName
	eventHandler:$d{!ui-event event:onenter action:$c{searchAct}}
!ui-text:searchDescription caption:$m{workflow.wfdoc.description} binding:searchDescription
	eventHandler:$d{!ui-event event:onenter action:$c{searchAct}}

// Table columns
!ui-label:wfCategoryDesc caption:$m{workflow.wfdoc.category} binding:wfCategoryDesc columnStyle:$s{width:120px;} sortable:true
!ui-label:name caption:$m{workflow.wfdoc.name} binding:name columnStyle:$s{width:120px;} sortable:true
!ui-label:description caption:$m{workflow.wfdoc.description} binding:description  columnStyle:$s{width:250px;} sortable:true

// Form components   
!ui-select:frmWfCategoryId caption:$m{workflow.wfdoc.category} list:wfcategorylist blankOption:$s{}
	binding:wfCategoryId required:true focus:true
!ui-name:frmName caption:$m{workflow.wfdoc.name} binding:name case:upper minLen:3 maxLen:32 required:true
!ui-text:frmDescription caption:$m{workflow.wfdoc.description} binding:description minLen:1 maxLen:48 size:32 required:true

!ui-wfdocclassifiercrudpanel:frmWfDocClassifierListPanel
!ui-wfdocattachmentcrudpanel:frmWfDocAttachmentListPanel
