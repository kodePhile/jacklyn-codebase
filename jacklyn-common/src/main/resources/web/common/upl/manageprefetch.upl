// UPL generic base template for managing prefetched records (Default)
// Copyright 2018-2020 The Code Department.
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
!ui-page caption:$s{} components:$c{manageRecordPanel}
    tableSerialNumbers:true tablePagination:true
    tableRowEventHandler:$d{!ui-event event:ondblclick action:$c{prepareViewAct}}

!ui-panel:manageRecordPanel style:$s{height:100%;}
    layout:$d{!ui-vertical style:$s{width:100%;height:100%;margin-bottom:-2px;} heights:$l{100%}}
    components:$c{manageBodyPanel}

!ui-switchpanel:manageBodyPanel style:$s{height:100%;} components:$c{searchBodyPanel prefetchItemPanel}
    components:$f{moreSwitchBodyPanels}

// Search Panel
!ui-panel:searchBodyPanel style:$s{height:100%;}
    layout:$d{!ui-vertical style:$s{table-layout:fixed;width:100%;height:100%;} heights:$l{none 100%}}
    components:$c{detailsPanel searchResultPanel}

!ui-shadedlistpanel:detailsPanel shadedDetailsList:$g{shadedDetailsList}
    style:$s{margin-top:4px; margin-left:4px; display:block;}

!ui-panel:searchResultPanel style:$s{height:100%;}
    layout:$d{!ui-horizontal style:$s{width:100%;height:100%;} heights:$l{100% 100%} widths:$l{none 148px}}
    components:$c{tablePanel actionsGrpPanel}

!ui-tablepanel:tablePanel style:$s{height:100%;}
    layout:$d{!ui-vertical style:$s{table-layout:fixed;width:100%;height:100%;}  heights:$l{100%}}
    tableSerialNumbers:$f{tableSerialNumbers} tableBinding:recordList tableColumnList:$g{tableColumnList}
    tableRowSelectable:true tableRowEventHandler:$f{tableRowEventHandler}
    tableSelDependentList:$c{editTblBtn viewTblBtn}
    tableWindowed:true tablePagination:$f{tablePagination}
    tableMultiSelect:$f{tableMultiSelect} tableMultiSelDependentList:$f{tableMultiSelDependentList}
    tableSortable:true
    tableHeaderEllipsis:$f{tableHeaderEllipsis}
    tableContentEllipsis:$f{tableContentEllipsis}
    tableSelDependentList:$f{moreTableSelDependentList}
   
!ui-panel:actionsGrpPanel layout:$d{!ui-vertical style:$s{margin:0 auto;}} style:$s{width:100%;}
    components:$c{actionPanel reportPanel}
!ui-panel:actionPanel layout:$d{!ui-vertical style:$s{width:100%;}} style:$s{margin-bottom:4px;}
    components:$c{viewTblBtn editTblBtn}
    components:$f{moreTableActionComponentList}
    components:$c{doneTblBtn}
!ui-panel:reportPanel layout:$d{!ui-vertical style:$s{width:100%;}}
    components:$c{reportType reportBtn}

// Maintain Panel
!ui-panel:prefetchItemPanel style:$s{height:100%;}
    layout:$d{!ui-horizontal style:$s{width:100%;height:100%;} heights:$l{100% 100%} widths:$l{none 148px}}
    components:$c{prefetchItemBasePanel prefetchActionsGrpPanel}

!ui-panel:prefetchItemBasePanel style:$s{height:100%;} layout:$d{!ui-vertical style:$s{table-layout:fixed;width:100%;height:100%;} heights:$l{none 100%}}
    components:$c{maintainTypeLabelPanel prefetchItemViewPanel}

// Top Label Panel
!ui-panel:maintainTypeLabelPanel layout:$d{!ui-horizontal style:$s{width:100%;}} components:$c{maintainTypeLabel itemOfLabel}
!ui-label:maintainTypeLabel styleClass:$e{jk-maintaintype} binding:modeDescription styleClassBinding:modeStyle
!ui-label:itemOfLabel styleClass:$e{jk-countlabel} binding:itemCountLabel

// Pre-fetch Action Panel
!ui-panel:prefetchActionsGrpPanel layout:$d{!ui-vertical style:$s{margin:0 auto;}} style:$s{width:100%;}
    components:$c{topActionPanel bottomActionPanel}
!ui-panel:topActionPanel layout:$d{!ui-vertical style:$s{width:100%;}} style:$s{margin-bottom:4px;}
    components:$c{firstFrmBtn prevFrmBtn nextFrmBtn lastFrmBtn}
    components:$f{moreCrudHeaderComponentList}
!ui-panel:bottomActionPanel layout:$d{!ui-vertical style:$s{width:100%;}}
    components:$c{saveNextFrmBtn saveCloseFrmBtn}
    components:$c{cancelFrmBtn doneFrmBtn}
    components:$f{moreCrudFooterComponentList}

//Common components
!ui-hidden:frmId binding:id

//Action buttons    
!ui-button:editTblBtn styleClass:$e{jk-button} privilege:$f{editPrivilege} caption:$m{button.edit}
    eventHandler:$d{!ui-event event:onclick action:$c{prepareEditAct}}
!ui-button:viewTblBtn styleClass:$e{jk-button} privilege:$f{viewPrivilege} caption:$m{button.view}
    eventHandler:$d{!ui-event event:onclick action:$c{prepareViewAct}}
!ui-button:doneTblBtn styleClass:$e{jk-button-alert} caption:$m{button.close}
    eventHandler:$d{!ui-event event:onclick action:$c{manageDoneAct}}

//Report buttons
!ui-hidden:reportType binding:reportType
!ui-button:reportBtn styleClass:$e{jk-button} caption:$m{button.report}
    eventHandler:$d{!ui-event event:onclick action:$c{preparePdfRpt}} hint:$m{hint.report}

!ui-button:firstFrmBtn styleClass:$e{jk-button} caption:$m{button.first}
    eventHandler:$d{!ui-event event:onclick action:$c{firstAct}}
!ui-button:prevFrmBtn styleClass:$e{jk-button} caption:$m{button.prev}
    eventHandler:$d{!ui-event event:onclick action:$c{prevAct}}
!ui-button:nextFrmBtn styleClass:$e{jk-button} caption:$m{button.next}
    eventHandler:$d{!ui-event event:onclick action:$c{nextAct}}
!ui-button:lastFrmBtn styleClass:$e{jk-button} caption:$m{button.last}
    eventHandler:$d{!ui-event event:onclick action:$c{lastAct}}
        
!ui-button:saveCloseFrmBtn styleClass:$e{jk-button} caption:$m{button.saveandclose}
    eventHandler:$d{!ui-event event:onclick action:$c{saveCloseAct}}
!ui-button:saveNextFrmBtn styleClass:$e{jk-button} caption:$m{button.saveandnext}
    eventHandler:$d{!ui-event event:onclick action:$c{saveNextAct}}
!ui-button:cancelFrmBtn styleClass:$e{jk-button-alert} caption:$m{button.cancel}
    eventHandler:$d{!ui-event event:onclick action:$c{cancelAct}}
!ui-button:doneFrmBtn styleClass:$e{jk-button-alert} caption:$m{button.close}
    eventHandler:$d{!ui-event event:onclick action:$c{doneAct}}
   
//CRUD actions
!ui-post:prepareEditAct path:$n{/prepareUpdateRecord} components:$c{tablePanel} debounce:true
!ui-post:prepareViewAct path:$n{/prepareViewRecord} components:$c{tablePanel} debounce:true
!ui-post:firstAct path:$n{/firstRecord} components:$f{moreNavigationPushComponents} debounce:true
!ui-post:prevAct path:$n{/previousRecord} components:$f{moreNavigationPushComponents} debounce:true
!ui-post:nextAct path:$n{/nextRecord} components:$f{moreNavigationPushComponents} debounce:true
!ui-post:lastAct path:$n{/lastRecord} components:$f{moreNavigationPushComponents} debounce:true
!ui-post:saveNextAct path:$n{/updateAndNextRecord} components:$c{prefetchItemPanel} debounce:true
    validations:$c{prefetchItemViewerVal} validations:$f{saveValidations}
!ui-post:saveCloseAct path:$n{/updateAndCloseRecord} components:$c{prefetchItemPanel} debounce:true
    validations:$c{prefetchItemViewerVal} validations:$f{saveValidations}
!ui-post:doneAct path:$n{/done} components:$f{moreNavigationPushComponents}
!ui-post:cancelAct path:$n{/cancel}

// Validations
!ui-validation:prefetchItemViewerVal components:$c{prefetchItemViewPanel}

//Other actions
!ui-post:manageDoneAct path:$n{/closePage}

//Report actions 
!ui-post:preparePdfRpt path:$n{/prepareGenerateReport} components:$c{reportType} valueList:$l{PDF}
