// UPL template for managing roles
// Copyright 2018 The Code Department
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
!ui-page caption:$m{security.role.manage}
	searchByList:$c{searchName searchDescription searchStatus}
    searchClearList:$c{searchName searchDescription searchStatus}
	tableColumnList:$c{name description activeAfter activeBefore themeDesc statusDesc}
	formSection:$d{!ui-section caption:$m{security.role.section.basicdetails} components:$c{frmName frmDescription frmApplication frmTheme frmStatus}}
	formSection:$d{!ui-section caption:$m{security.role.section.schedule} components:$c{frmActiveAfter frmActiveBefore}}
	formSection:$d{!ui-section caption:$m{security.role.section.privileges} components:$c{frmPrivilegeAttachment}}
	formSection:$d{!ui-section caption:$m{security.role.section.steps} components:$c{frmStepAttachment}}
	formBinding:largeData
	addPrivilege:$s{sec-role-add} editPrivilege:$s{sec-role-edit}
	viewPrivilege:$s{sec-role-view} deletePrivilege:$s{sec-role-delete}
    addValidations:$c{frmNameVal frmDescVal}
    saveValidations:$c{frmNameVal frmDescVal}

//Search
!ui-text:searchName caption:$m{security.role.name} case:upper binding:searchName
    eventHandler:$d{!ui-event event:onenter action:$c{searchAct}}
!ui-text:searchDescription caption:$m{security.role.description} binding:searchDescription
    eventHandler:$d{!ui-event event:onenter action:$c{searchAct}}
!ui-select:searchStatus caption:$m{security.role.status} binding:searchStatus list:recordstatuslist blankOption:$s{}
	eventHandler:$d{!ui-event event:onchange action:$c{searchAct}}

//Table columns
!ui-label:name caption:$m{security.role.name} binding:name  columnStyle:$s{width:120px;} sortable:true
!ui-label:description caption:$m{security.role.description} binding:description  columnStyle:$s{width:200px;} sortable:true
!ui-label:activeAfter caption:$m{security.role.activeafter} binding:activeAfter style:$s{text-align:center;} formatter:$d{!timeformat style:short}  columnStyle:$s{width:120px;}  sortable:true
!ui-label:activeBefore caption:$m{security.role.activebefore} binding:activeBefore style:$s{text-align:center;} formatter:$d{!timeformat style:short} columnStyle:$s{width:120px;}  sortable:true
!ui-label:themeDesc caption:$m{security.role.theme} binding:themeDesc columnStyle:$s{width:150px;} sortable:true
!ui-label:statusDesc caption:$m{security.role.status} binding:statusDesc style:$s{text-align:center;} columnStyle:$s{width:100px;} sortable:true

//Form components   
!ui-name:frmName caption:$m{security.role.name} case:upper binding:data.name focus:true dollar:true underscore:true minLen:3 maxLen:32 required:true
!ui-text:frmDescription caption:$m{security.role.description} binding:data.description maxLen:48 required:true
!ui-time:frmActiveAfter caption:$m{security.role.activeafter} binding:data.activeAfter clearable:true
!ui-time:frmActiveBefore caption:$m{security.role.activebefore} binding:data.activeBefore clearable:true
!ui-select:frmApplication caption:$m{security.role.application} binding:data.application list:webapplicationlist blankOption:$s{}
!ui-select:frmTheme caption:$m{security.role.theme} binding:data.themeId list:themelist blankOption:$s{}
!ui-select:frmStatus caption:$m{security.role.status} binding:data.status list:recordstatuslist blankOption:$s{} required:true

//Form attachments
!ui-panel:frmPrivilegeAttachment layout:$d{!ui-vertical} components:$c{privAssignmentBox}
!ui-assignmentbox:privAssignmentBox binding:privilegeIdList
    filterCaption1:$m{security.role.maintain.assigned.privilegecategory}
    filterCaption2:$m{security.role.maintain.assigned.module}
    assignCaption:$m{security.role.maintain.assigned.privilege} unassignCaption:$m{security.role.maintain.available.privilege}
    filterList1:$s{privilegecategorylist}
    filterList2:$s{modulelist}
    assignList:$s{privilegeinlist} unassignList:$s{privilegenotinlist}
    
!ui-panel:frmStepAttachment layout:$d{!ui-vertical} components:$c{queAssignmentBox}
!ui-assignmentbox:queAssignmentBox binding:wfStepIdList
    filterCaption1:$m{security.role.maintain.assigned.category}
    filterCaption2:$m{security.role.maintain.assigned.template}
    assignCaption:$m{security.role.maintain.assigned.step}
    unassignCaption:$m{security.role.maintain.available.step}
    filterList1:$s{wfcategorylist} filterList2:$s{wftemplatelist}
    assignList:$s{wfstepinlist} unassignList:$s{wfstepnotinlist}
    
//More action components

//Validations
!ui-uniquevalidation:frmNameVal components:$c{frmName}
!ui-uniquevalidation:frmDescVal components:$c{frmDescription}
