// UPL template for managing service applications
// Copyright 2018 The Code Department
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
!ui-page caption:$m{system.clientapp.manage}
    searchByList:$c{searchName searchDescription searchStatus}
    searchClearList:$c{searchName searchDescription searchStatus}
    tableColumnList:$c{name description typeDesc statusDesc}
    formSection:$d{!ui-section caption:$m{system.clientapp.section.basicdetails} components:$c{frmName frmDescription frmType frmStatus}}
    formSection:$d{!ui-section caption:$m{system.clientapp.section.assets} components:$c{frmAssetAttachment}}
    formBinding:largeData
    addPrivilege:$s{sys-clientappadd} editPrivilege:$s{sys-clientappedit}
    viewPrivilege:$s{sys-clientappview} deletePrivilege:$s{sys-clientappdelete}
    addValidations:$c{frmNameVal frmDescVal}
    saveValidations:$c{frmNameVal frmDescVal}

//Search
!ui-text:searchName caption:$m{system.clientapp.name} case:upper binding:searchName
    eventHandler:$d{!ui-event event:onenter action:$c{searchAct}}
!ui-text:searchDescription caption:$m{system.clientapp.description} binding:searchDescription
    eventHandler:$d{!ui-event event:onenter action:$c{searchAct}}
!ui-select:searchStatus caption:$m{system.clientapp.status} binding:searchStatus list:recordstatuslist blankOption:$s{}
    eventHandler:$d{!ui-event event:onchange action:$c{searchAct}}

//Table columns
!ui-label:name caption:$m{system.clientapp.name} binding:name  columnStyle:$s{width:120px;} sortable:true
!ui-label:description caption:$m{system.clientapp.description} binding:description  columnStyle:$s{width:200px;} sortable:true
!ui-label:typeDesc caption:$m{system.clientapp.type} binding:typeDesc style:$s{text-align:center;} columnStyle:$s{width:100px;} sortable:true
!ui-label:statusDesc caption:$m{system.clientapp.status} binding:statusDesc style:$s{text-align:center;} columnStyle:$s{width:100px;} sortable:true

//Form components   
!ui-name:frmName caption:$m{system.clientapp.name} binding:data.name focus:true
    dollar:true underscore:true period:true dash:true minLen:3 maxLen:32 required:true
!ui-text:frmDescription caption:$m{system.clientapp.description} binding:data.description maxLen:48 size:40 required:true
!ui-select:frmType caption:$m{system.clientapp.type} binding:data.type readOnly:true list:clientapptypelist blankOption:$s{} required:true
!ui-select:frmStatus caption:$m{system.clientapp.status} binding:data.status list:recordstatuslist blankOption:$s{} required:true

!ui-panel:frmAssetAttachment layout:$d{!ui-vertical} components:$c{assetAssignmentBox}
!ui-assignmentbox:assetAssignmentBox binding:systemAssetIdList
    filterCaption1:$m{system.clientapp.type}
    filterCaption2:$m{system.clientapp.module}
    assignCaption:$m{system.clientapp.asset.assigned}
    unassignCaption:$m{system.clientapp.asset.available}
    filterList1:$s{systemassettypelist} filterList2:$s{modulelist}
    assignList:$s{systemassetinlist} unassignList:$s{systemassetnotinlist}

//Validations
!ui-uniquevalidation:frmNameVal components:$c{frmName}
!ui-uniquevalidation:frmDescVal components:$c{frmDescription}
