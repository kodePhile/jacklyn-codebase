// UPL template for managing user input definitions
// Copyright 2018 The Code Department
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
!ui-page caption:$m{system.inputctrldef.manage}
	searchByList:$c{searchModuleId searchName searchDescription searchStatus}
    searchClearList:$c{searchModuleId searchName searchDescription searchStatus}
	tableColumnList:$c{moduleDesc name description control statusDesc}
	formSection:$d{!ui-section caption:$m{system.inputctrldef.section.basicdetails} components:$c{moduleId frmName frmDescription frmControl frmStatus}}
	addPrivilege:$s{sys-inputctrldefadd} editPrivilege:$s{sys-inputctrldefedit}
	viewPrivilege:$s{sys-inputctrldefview} deletePrivilege:$s{sys-inputctrldefdelete}
    addValidations:$c{frmNameVal frmDescVal frmUplDescVal}
    saveValidations:$c{frmNameVal frmDescVal frmUplDescVal}

//Search
!ui-text:searchName caption:$m{system.inputctrldef.name} binding:searchName eventHandler:$d{!ui-event event:onenter action:$c{searchAct}}
!ui-text:searchDescription caption:$m{system.inputctrldef.description} binding:searchDescription eventHandler:$d{!ui-event event:onenter action:$c{searchAct}}
!ui-select:searchStatus caption:$m{system.inputctrldef.status} binding:searchStatus list:recordstatuslist blankOption:$s{}
		eventHandler:$d{!ui-event event:onchange action:$c{searchAct}}
!ui-select:searchModuleId caption:$m{system.sysparameter.module} binding:searchModuleId list:modulelist blankOption:$s{}
		eventHandler:$d{!ui-event event:onchange action:$c{searchAct}}

//Table columns
!ui-label:moduleDesc caption:$m{system.inputctrldef.module} binding:moduleDesc columnStyle:$s{width:100px;} sortable:true
!ui-label:name caption:$m{system.inputctrldef.name} binding:name columnStyle:$s{width:80px;} sortable:true
!ui-label:description caption:$m{system.inputctrldef.description} binding:description  columnStyle:$s{width:250px;} sortable:true
!ui-label:control caption:$m{system.inputctrldef.control} binding:control  columnStyle:$s{width:250px;} sortable:true
!ui-label:statusDesc caption:$m{system.inputctrldef.status} binding:statusDesc style:$s{text-align:center;} columnStyle:$s{width:100px;} sortable:true

//Form components   
!ui-select:moduleId caption:$m{system.inputctrldef.module} binding:moduleId list:modulelist blankOption:$s{}
!ui-name:frmName caption:$m{system.inputctrldef.name} binding:name minLen:3 maxLen:32 required:true
!ui-text:frmDescription caption:$m{system.inputctrldef.description} binding:description minLen:1 maxLen:48 required:true size:40
!ui-text:frmControl caption:$m{system.inputctrldef.control} binding:control maxLen:80 required:true size:56
!ui-select:frmStatus caption:$m{system.inputctrldef.status} binding:status list:recordstatuslist blankOption:$s{} required:true

//Validations
!ui-uniquevalidation:frmNameVal components:$c{frmName}
!ui-uniquevalidation:frmDescVal components:$c{frmDescription}
!ui-upldescriptorvalidation:frmUplDescVal components:$c{frmControl}
